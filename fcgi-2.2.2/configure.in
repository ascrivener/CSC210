dnl     $Id: configure.in,v 1.20 2001/11/27 16:27:13 robs Exp $
dnl
dnl     This file is an input file used by the GNU "autoconf" program to
dnl     generate the file "configure", which is run during the build
dnl     to configure the system for the local environment.

AC_INIT
AM_INIT_AUTOMAKE(fcgi, 2.2.2)

AM_CONFIG_HEADER(fcgi_config.h)

AC_PROG_CC
AC_PROG_CPP 
AC_PROG_INSTALL 
AC_PROG_LIBTOOL

AC_PROG_CXX

dnl autoconf defaults CXX to 'g++', so its unclear whether it exists/works
AC_MSG_CHECKING([whether $CXX works])
AC_LANG([C++])
AC_TRY_COMPILE([#include <iostream.h>], [istream is(NULL);], 
               [CXX_WORKS=1]
               [AC_MSG_RESULT(yes)], 
               [AC_MSG_RESULT(no)])
AC_LANG([C])

if test -n "${CXX_WORKS}"; then
	LIBFCGIXX=libfcgi++.la
	ECHO_CPP=echo-cpp${EXEEXT}
fi
AC_SUBST(LIBFCGIXX)
AC_SUBST(ECHO_CPP)

AC_CHECK_LIB([nsl],       [main]) ac_cv_lib_nsl=ac_cv_lib_nsl_main
AC_CHECK_LIB([resolv],    [main]) ac_cv_lib_resolv=ac_cv_lib_resolv_main
AC_CHECK_LIB([socket], 	  [main]) ac_cv_lib_socket=ac_cv_lib_socket_main
AC_CHECK_LIB([dnet_stub], [main]) ac_cv_lib_dnet_stub=ac_cv_lib_dnet_stub_main
AC_CHECK_LIB([ieee], 	  [main]) ac_cv_lib_ieee=ac_cv_lib_ieee_main

AC_SUBST(threaded)
AC_CHECK_LIB([pthread],	  [main], 
	[AC_DEFINE([HAVE_LIBPTHREAD], [1],
			   [Define if you have the pthread library])
	 AC_SUBST([HAVE_LIBPTHREAD])
         threaded='threaded$(EXEEXT)']) 
ac_cv_lib_pthread=ac_cv_lib_pthread_main

FCGI_COMMON_CHECKS

AC_REPLACE_FUNCS([strerror])

AC_C_INLINE

#--------------------------------------------------------------------
#  This is a little hokie in that it avoids including config.guess
#  and config.sub in the distribution, but its been working so far.
#  Windows builds don't run configure so we should be safe fixing
#  this to 'unix' (at least for now).
#--------------------------------------------------------------------
SYSTEM=unix
AC_SUBST([SYSTEM])

if test "$GCC"; then
	CFLAGS="$CFLAGS -Wall -Wmissing-prototypes"
fi

AC_CONFIG_FILES(Makefile
		cgi-fcgi/Makefile
		include/Makefile
		libfcgi/Makefile
		examples/Makefile)

AC_OUTPUT
